plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'jacoco'
}

group = 'ro.unibuc'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility(JavaVersion.VERSION_21)
    targetCompatibility(JavaVersion.VERSION_21)
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    snippetsDir = file("build/generated-snippets")
    testcontainersVersion = "1.15.3"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Swagger
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.0.2'

    // JWT
    implementation("io.jsonwebtoken:jjwt-api:0.11.2")
    runtimeOnly("io.jsonwebtoken:jjwt-impl:0.11.2")
    runtimeOnly("io.jsonwebtoken:jjwt-jackson:0.11.2")

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.projectlombok:lombok:1.18.28'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'

    // Test config
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    implementation 'org.springframework.session:spring-session-core'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
    runtimeOnly "org.junit.vintage:junit-vintage-engine:5.8.2"
    testImplementation 'io.cucumber:cucumber-core:6.8.0'
    testImplementation 'io.cucumber:cucumber-java:6.8.0'
    testImplementation 'io.cucumber:cucumber-junit:6.8.0'
    testImplementation 'io.cucumber:cucumber-spring:6.8.0'
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform {
        excludeTags ("IT", "E2E")
    }
    finalizedBy jacocoTestReport
}

tasks.named('jacocoTestReport') {
	  dependsOn test
	  reports {
		  xml.required = true
		  html.required = true
		  csv.required = false
	}
}

tasks.register('testIT', Test) {
    testLogging.showStandardStreams = true
    outputs.dir snippetsDir
    outputs.upToDateWhen { false }

    useJUnitPlatform {
        includeTags "IT"
    }
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

tasks.register('testE2E') {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin', 'pretty',
                    '--plugin', 'html:target/cucumber-report.html',
                    '--glue', 'ro.unibuc.hello.e2e.steps',
                    'src/test/resources'
            ]
        }
    }
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}
